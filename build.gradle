// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    alias libs.plugins.android.application apply false
    alias libs.plugins.android.library apply false
    alias libs.plugins.kotlin.android apply false
    alias libs.plugins.kotlin.kapt apply false
    alias libs.plugins.kotlin.parcelize apply false
    alias libs.plugins.kotlin.serialization apply false
    alias libs.plugins.dager.hilt apply false
    alias libs.plugins.maps.secret apply false
    alias libs.plugins.spotless apply false
    alias libs.plugins.version.check
    alias libs.plugins.version.update
}

versionCatalogUpdate {
    sortByKey = true
    pin {
        versions = ["desugar-jdk"]
    }
    keep {
//        keepUnusedVersions = true
//        keepUnusedLibraries = true
//        keepUnusedPlugins = true
    }
}

def isNonStable = { version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    resolutionStrategy {
        componentSelection {
            all {
                if (isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)) {
                    reject('Release candidate')
                }
            }
        }
    }
}
